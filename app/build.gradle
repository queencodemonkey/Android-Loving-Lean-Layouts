apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

ext.versions = [
        // Android
        targetSdkVersion: 26,
        minSdkVersion   : 19,
        buildtools      : "26.0.0",
        support         : "25.4.0",

        // Components
        constraintlayout: "1.0.2",

        // Logging/Monitoring
        timber          : "4.5.1",

        // UI
        calligraphy     : "2.3.0",

        // Utility
        butterknife     : "8.6.0"
]

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0


android {
    compileSdkVersion versions.targetSdkVersion
    buildToolsVersion versions.buildtools
    defaultConfig {
        applicationId "com.randomlytyping.lovingleanlayouts"
        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Android
    implementation "com.android.support:appcompat-v7:$versions.support"
    implementation "com.android.support:cardview-v7:$versions.support"
    implementation "com.android.support:support-annotations:$versions.support"

    // Components
    implementation "com.android.support:recyclerview-v7:$versions.support"
    implementation "com.android.support.constraint:constraint-layout:$versions.constraintlayout"
    implementation "com.android.support.constraint:constraint-layout-solver:$versions.constraintlayout"

    // Utility
    implementation "com.jakewharton:butterknife:$versions.butterknife"
    annotationProcessor "com.jakewharton:butterknife-compiler:$versions.butterknife"

    // UI
    implementation "uk.co.chrisjenx:calligraphy:$versions.calligraphy"

    // Logging
    implementation "com.jakewharton.timber:timber:$versions.timber"

    // Testing
    androidTestImplementation ('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
}
